import java_cup.runtime.Symbol;
import java.util.List;
import java.util.ArrayList;
import org.slf4j.Logger;
import org.slf4j.LoggerFactory;

import ast.*;

parser code
{:
    private static final Logger LOGGER = LoggerFactory.getLogger(parser.class);

    /** Report a non fatal error (or warning).
     *
     * @param message an error message.
     * @param info    an extra object reserved for use by specialized subclasses.
     */
    public void report_error(String message, Object info)
    {
        String error = "";
        if (!(info instanceof Symbol))
            info = cur_token;
        if(info instanceof Symbol) {
            int line = ((Symbol)info).left;
            int column = ((Symbol)info).right;
            error = line  + ":" + column;
        }
        error += ": " + message;
        LOGGER.error(error);
    }

    /** Report a fatal error.
     *
     * @param message an error message.
     * @param info    an extra object reserved for use by specialized subclasses.
     */
    public void report_fatal_error(String   message, Object   info) throws java.lang.Exception
    {
        /* stop parsing (not really necessary since we throw an exception, but) */
        done_parsing();

        /* use the normal error message reporting to put out the message */
        report_error(message, info);

        /* throw an exception */
        throw new Exception("Can't recover from previous error(s)");
    }

    public int getLine() {
        Symbol symbol = (Symbol) cur_token;
        return symbol.left;
    }

    public int getColumn() {
        Symbol symbol = (Symbol) cur_token;
        return symbol.right;
    }
:};

terminal AND, OR, NOT;
terminal PUTS;
terminal CHAR;
terminal INT;
terminal DO, WHILE;
terminal FUN;
terminal IF, ELSE, THEN;
terminal NOTHING;
terminal REF;
terminal RETURN;
terminal VAR;
terminal java.lang.String IDENTIFIER;       // name

terminal LPAREN, RPAREN, SEMICOLON, COLON, LCURLY, RCURLY, LBRACKET, RBRACKET, COMMA;
terminal EQ, NEQ, LEQ, GEQ, LTHAN, GTHAN;
terminal SPACER;
terminal TIMES, DIV, MOD;
terminal PLUS, MINUS;

terminal java.lang.Integer    INTEGER_LITERAL;
terminal java.lang.String     STRING_LITERAL;
terminal java.lang.Character  CHAR_LITERAL;

non terminal Program program;
non terminal Definition func_def;
non terminal List<Definition> local_defs;
non terminal Definition local_def;
non terminal List<Definition> fpar_defs;
non terminal Definition fpar_def;
non terminal Type ret_type;
non terminal List<String> ids;
non terminal Type fpar_type;
non terminal Type data_type;
non terminal Type type;
non terminal int_consts;
non terminal Definition func_decl;
non terminal Definition var_def;
non terminal List<Statement> stmts;
non terminal Statement stmt;
non terminal func_call;
non terminal Condition cond;
non terminal List<Expression> exprs;
non terminal Expression expr;

precedence left OR;
precedence left AND;
precedence nonassoc NOT;
precedence nonassoc EQ, GTHAN, LTHAN, LEQ, GEQ, NEQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIV, MOD;
precedence left THEN;
precedence left ELSE;

start with program;
       
program ::= /* empty */
          {:
            RESULT = new Program();
            RESULT.setLine(1);
            RESULT.setColumn(1);
          :}
          | func_def:fd
          {:
            RESULT = new Program(fd);
            RESULT.setLine(fdleft);
            RESULT.setColumn(fdright);
          :} 
          ;

func_def ::= FUN:f IDENTIFIER:i LPAREN:lp fpar_defs:fds RPAREN COLON ret_type:rt local_defs:lds LCURLY stmts:ss RCURLY 
            {:
                RESULT = new FunctionDefinition(rt, i, fds, lds, ss);
                RESULT.setLine(fleft);
                RESULT.setColumn(fright);
            :}
           | FUN:f IDENTIFIER:i LPAREN:lp fpar_defs:fds RPAREN COLON ret_type:rt LCURLY stmts:ss RCURLY
            {:
                RESULT = new FunctionDefinition(rt, i, fds, new ArrayList<Definition>(), ss);
                RESULT.setLine(fleft);
                RESULT.setColumn(fright);
            :}
           ;

fpar_defs ::= fpar_def:f SEMICOLON fpar_defs:fs
            {:
              fs.add(f);
              RESULT = fs;
            :}
            | fpar_def:f
            {:
              RESULT = new ArrayList<Definition>();
              RESULT.add(f);
            :}
            | /* empty */
            {:
              RESULT = new ArrayList<Definition>();
            :}
            ;

fpar_def ::= REF:r ids:i COLON fpar_type:ft
            {:
              RESULT = new FunctionParameterDefinition(ft, i, true);
              RESULT.setLine(rleft);
              RESULT.setColumn(rright);
            :}
           | ids:i COLON fpar_type:ft   
            {:
              RESULT = new FunctionParameterDefinition(ft, i, false);
              RESULT.setLine(ftleft);
              RESULT.setColumn(ftright);
            :}
           ;

ids      ::= IDENTIFIER:i COMMA ids:is
              {:
                 is.add(i);
                 RESULT = is;
              :}
           | IDENTIFIER:i
              {:
                 RESULT = new ArrayList<String>();
                 RESULT.add(i);
              :}
           ;

data_type ::= INT:i
            {:
              RESULT = Type.INT;
            :}
            | CHAR:c
            {:
              RESULT = Type.CHAR;
            :}
            ;

type ::= data_type int_consts ; 

int_consts ::= 
             | LBRACKET INTEGER_LITERAL RBRACKET int_consts
             ;

ret_type ::= data_type:d
            {:
              RESULT = d;
            :}
           | NOTHING:n
            {:
                RESULT = Type.NOTHING;
            :}
           ;

fpar_type ::= data_type LBRACKET RBRACKET int_consts
            | data_type int_consts
            ;

local_defs ::= local_def:l
                {:
                RESULT = new ArrayList<Definition>();
                RESULT.add(l);
                :}
             | local_defs:ls local_def:l
                {:
                ls.add(l);
                RESULT = ls;
                :}
             ;

local_def ::= func_def:fd
            {:
              RESULT = fd;
            :}
            | func_decl:fl
            {:
              RESULT = fl;
            :}
            | var_def:vd
            {:
              RESULT = vd;
            :}
            ;

func_decl ::= FUN:f IDENTIFIER:i LPAREN:lp fpar_defs:fds RPAREN COLON ret_type:rt local_defs:lds SEMICOLON 
            {:
                RESULT = new FunctionDeclaration(rt, i, fds, lds);
                RESULT.setLine(fleft);
                RESULT.setColumn(fright);
            :}
            | FUN:f IDENTIFIER:i LPAREN:lp fpar_defs:fds RPAREN COLON ret_type:rt SEMICOLON
            {:
                RESULT = new FunctionDeclaration(rt, i, fds, new ArrayList<Definition>());
                RESULT.setLine(fleft);
                RESULT.setColumn(fright);
            :}
            ;

var_def ::= VAR ids COLON type SEMICOLON ;

stmts ::= stmt:s
        {:
          RESULT = new ArrayList<Statement>();
          RESULT.add(s);
        :}
        | stmts:sl stmt:s
        {:
          sl.add(s);
          RESULT = sl;
        :}
        ;

stmt ::= SEMICOLON
       | PUTS:p LPAREN expr:e RPAREN SEMICOLON
         {:
         RESULT = new PutsStatement(e);
         RESULT.setLine(pleft);
         RESULT.setColumn(pright);
         :}
       | IDENTIFIER SPACER expr SEMICOLON
       | STRING_LITERAL SPACER expr SEMICOLON
       | LCURLY stmts:ss RCURLY
       | func_call SEMICOLON
       | IF:i expr:e THEN stmt:s 
       {:
         RESULT = new IfStatement(e, s);
         RESULT.setLine(ileft);
         RESULT.setColumn(iright);
       :}
       | IF:i cond:c THEN stmt:s ELSE stmt:ss
       | WHILE cond DO stmt
       | RETURN expr SEMICOLON
       | RETURN SEMICOLON
       ;

func_call ::= IDENTIFIER LPAREN RPAREN
            | IDENTIFIER LPAREN exprs RPAREN
            ; 

exprs ::= expr COMMA exprs
        | expr
        ;

expr ::= INTEGER_LITERAL:l
         {:
             RESULT = new IntegerLiteralExpression(l);
             RESULT.setLine(lleft);
             RESULT.setColumn(lright);
         :}
       | CHAR_LITERAL:c
         {:
             RESULT = new CharLiteralExpression(c);
             RESULT.setLine(cleft);
             RESULT.setColumn(cright);
         :}
       | IDENTIFIER:i
           {:
             RESULT = new IdentifierExpression(i);
             RESULT.setLine(ileft);
             RESULT.setColumn(iright);
           :}
       | STRING_LITERAL:s
           {:
             RESULT = new StringLiteralExpression(s);
             RESULT.setLine(sleft);
             RESULT.setColumn(sright);
           :}
       | IDENTIFIER LBRACKET expr RBRACKET
       | STRING_LITERAL LBRACKET expr RBRACKET
       | func_call
       | LPAREN:lp expr:e RPAREN
         {:
             RESULT = new ParenthesisExpression(e);
             RESULT.setLine(lpleft);
             RESULT.setColumn(lpright);
         :}
       | PLUS:p expr:e
         {:
             RESULT = new UnaryExpression(Operator.PLUS, e);
             RESULT.setLine(pleft);
             RESULT.setColumn(pright);
         :}
       | MINUS:m expr:e
         {:
             RESULT = new UnaryExpression(Operator.MINUS, e);
             RESULT.setLine(mleft);
             RESULT.setColumn(mright);
         :}
       | expr:e1 PLUS expr:e2
         {:
             RESULT = new BinaryExpression(Operator.PLUS, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 MINUS expr:e2
         {:
             RESULT = new BinaryExpression(Operator.MINUS, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 TIMES expr:e2
         {:
             RESULT = new BinaryExpression(Operator.TIMES, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 DIV expr:e2
         {:
             RESULT = new BinaryExpression(Operator.DIV, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 MOD expr:e2
         {:
             RESULT = new BinaryExpression(Operator.MOD, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 EQ expr:e2
         {:
             RESULT = new BinaryExpression(Operator.EQ, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 NEQ expr:e2
         {:
             RESULT = new BinaryExpression(Operator.NEQ, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 LTHAN expr:e2
         {:
             RESULT = new BinaryExpression(Operator.LTHAN, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 GTHAN expr:e2
         {:
             RESULT = new BinaryExpression(Operator.GTHAN, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 LEQ expr:e2
         {:
             RESULT = new BinaryExpression(Operator.LEQ, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       | expr:e1 GEQ expr:e2
         {:
             RESULT = new BinaryExpression(Operator.GEQ, e1, e2);
             RESULT.setLine(e1left);
             RESULT.setColumn(e1right);
         :}
       ;

cond ::= LPAREN:lp cond:c RPAREN
         {:
             RESULT = new ParenthesisCondition(c);
             RESULT.setLine(lpleft);
             RESULT.setColumn(lpright);
         :}
       | NOT:n cond:c
         {:
             RESULT = new UnaryCondition(Operator.NOT, c);
             RESULT.setLine(nleft);
             RESULT.setColumn(nright);
         :}
       | cond:c1 AND cond:c2
         {:
             RESULT = new BinaryCondition(Operator.AND, c1, c2);
             RESULT.setLine(c1left);
             RESULT.setColumn(c1right);
         :}
       | cond:c1 OR cond:c2
         {:
             RESULT = new BinaryCondition(Operator.OR, c1, c2);
             RESULT.setLine(c1left);
             RESULT.setColumn(c1right);
         :}
       ;